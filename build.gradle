plugins {
  id 'org.springframework.boot'
  id 'org.jetbrains.kotlin.jvm' apply false
  id 'java-library'
  id 'maven-publish'
  id "com.avast.gradle.docker-compose" version "0.14.9"
  id 'io.spring.dependency-management' version '1.0.11.RELEASE'
}

group 'io.github.luraframework'
version '0.0.2-SNAPSHOT'


def jarProject = subprojects.findAll {
  it.name.endsWith("starter")
    || it.name == "lura-framework-core"
    || it.name == 'lura-framework-commons'
    || it.name == 'lura-framework-security-core'
}


// config maven publish
configure(jarProject) {
  apply plugin: 'java'
  apply plugin: 'io.spring.dependency-management'

  group rootProject.group
  version rootProject.version
  targetCompatibility = "1.8"

  dependencyManagement {
    imports {
      mavenBom "org.springframework.cloud:spring-cloud-dependencies:2020.0.4"
      mavenBom "com.alibaba.cloud:spring-cloud-alibaba-dependencies:2.2.6.RELEASE"
    }
  }


  repositories {
    mavenCentral()
    maven {
      url 'https://s01.oss.sonatype.org/content/repositories/snapshots/'
      mavenContent {
        snapshotsOnly()
      }
    }
  }

  dependencies {
    compileOnly 'org.projectlombok:lombok:1.18.12'
    annotationProcessor 'org.projectlombok:lombok:1.18.12'
  }


  apply from: "${rootProject.projectDir}/gradle/publish.gradle"

}

